repositories {
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'antlr'

dependencies {
    compile 'org.ow2.asm:asm:5.0.4'
    compile 'org.apache.commons:commons-lang3:3.4'
    testCompile 'junit:junit:4.12'
    antlr "org.antlr:antlr4:4.5" // use ANTLR version 4
}

generateGrammarSource {
    arguments += ["-visitor", "-long-messages"]
}

// By default, Antlr generates the source files in a directory that does not
// match the package. This task will move the generated source files to a
// directory matching the package.
generateGrammarSource.doLast {
    File generatedDir = file('build/generated-src/antlr/main')
    File packageDir = new File(generatedDir, 'jacob/simpson/tinyscript/grammar')
    packageDir.mkdirs()
    generatedDir.listFiles().each {File file ->
        file.renameTo(new File(packageDir, file.getName()))
    }
}

task(run, dependsOn: 'classes', type: JavaExec) {
    main = 'jacob.simpson.tinyscript.Main'
    classpath = sourceSets.main.runtimeClasspath
    args 'scripts/hello-world.ts'
}

//create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Tiny Script',
                'Implementation-Version': '1.0',
                'Main-Class': 'jacob.simpson.tinyscript.Main'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

defaultTasks 'run'
